# Fixed PKGBUILD from https://github.com/stefanpartheym/archlinux-ipu6-webcam
# Removed the udev rule that creates bunch of broken symlinks
# Original PKGBUILD: https://aur.archlinux.org/packages/intel-ipu6ep-camera-hal-git
_ipu_ver=ipu6ep
pkgname=intel-ipu6ep-camera-hal-git-fix
_pkgname=ipu6-camera-hal
pkgver=r56.884b81a
pkgrel=1
pkgdesc="Intel IPU6 camera HAL (Alder Lake)"
arch=('x86_64')
url="https://github.com/intel/ipu6-camera-hal"
license=('unknown')
depends=('intel-ipu6-dkms-git' 'intel-ipu6ep-camera-bin')
makedepends=('git' 'cmake')
provides=(ipu6-camera-hal)
conflicts=('intel-ipu6-camera-hal-git' 'intel-ipu6ep-camera-hal-git')
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cmake -B build -S "$_pkgname"       \
        -DCMAKE_BUILD_TYPE=Release      \
        -DIPU_VER=$_ipu_ver             \
        -DUSE_PG_LITE_PIPE=ON           \
        -DCMAKE_INSTALL_PREFIX="/usr"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}

