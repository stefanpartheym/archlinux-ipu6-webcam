# Fixed PKGBUILD from https://github.com/stefanpartheym/archlinux-ipu6-webcam
# Added required patches
# Original PKGBUILD: https://aur.archlinux.org/packages/v4l2loopback-dkms-git
_pkgbase=v4l2loopback
pkgname=${_pkgbase}-dkms-git-fix
pkgver=r10.cae2954
pkgrel=1
pkgdesc="v4l2-loopback device"
url="https://github.com/umlaeute/v4l2loopback"
arch=('any')
license=('GPL2')
depends=('dkms')
makedepends=('git' 'help2man')
conflicts=("${_pkgbase}-dkms" "${_pkgbase}-dkms-git")
provides=("${_pkgbase}-dkms")
source=("git://git.launchpad.net/ubuntu/+source/v4l2loopback#branch=ubuntu/devel")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/${_pkgbase}"
    (
        set -o pipefail
        printf "r%s.%s" "10" "$(git rev-parse --short HEAD)"
    )
}

prepare() {
    cd "${srcdir}/${_pkgbase}"
    for patch in debian/patches/*.patch; do
        patch -p1 -i "$patch"
    done;
}

package() {
    cd "${srcdir}/${_pkgbase}"
    mkdir -p "${pkgdir}/usr/share/licenses/${_pkgbase}"
    cp -vf COPYING "${pkgdir}/usr/share/licenses/${_pkgbase}"
    make DESTDIR="${pkgdir}" PREFIX="/usr" install-utils install-man
    mkdir -p "${pkgdir}/usr/src/${_pkgbase}-${pkgver}"
    cp -ar * "${pkgdir}/usr/src/${_pkgbase}-${pkgver}"
}

