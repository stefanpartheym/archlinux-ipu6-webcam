# Fixed PKGBUILD from https://github.com/stefanpartheym/archlinux-ipu6-webcam
# Uses earlier version of icamerasrc. The 1 Sep push broke it again for application access.
# Original PKGBUILD: https://aur.archlinux.org/packages/icamerasrc-git
pkgname=icamerasrc-git-fix
_pkgname=icamerasrc
pkgver=r63.feeea6e
pkgrel=1
pkgdesc="Intel IPU6 camera source for GStreamer"
arch=('x86_64')
url="https://github.com/intel/icamerasrc"
license=('LGPL2.1')
depends=('ipu6-camera-hal'
         'gstreamer'
         'libdrm'
         'gst-plugins-base')
makedepends=('git'
             'autoconf'
             'make'
             'gcc')
source=("git+${url}.git#commit=feeea6eed1bfd9baefa4a6ace2798e2eae3d3cce"
        "70-ipu6-psys.rules")
sha256sums=('SKIP'
            '6069b552eb464b69876f0c7dda153b82158d695c50e1f5929e88556d0017bde2')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$_pkgname"
    export CHROME_SLIM_CAMHAL=ON
    export STRIP_VIRTUAL_CHANNEL_CAMHAL=ON
    ./autogen.sh
    make -j8
}

package() {
    cd "$srcdir/$_pkgname"
    mkdir -p $pkgdir/usr/lib/udev/rules.d
    make DESTDIR="$pkgdir" install
    install -Dm644 $srcdir/70-ipu6-psys.rules $pkgdir/usr/lib/udev/rules.d
}
